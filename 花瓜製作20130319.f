\ 花瓜製作20130319.f 莊景芳 諶其騮 呂明貴 劉屏 陳爽 20130319
\ FORGET 啟動設定
: 啟動設定 DECIMAL
  5 PB OUTPUT     \ 設定 輸出 (HIGH 亮燈 提示 需 人工介入)
  6 PD OUTPUT     \ 設定 輸出 (HIGH 通電 控制加溫)
  7 PD INPUT      \ 設定 輸入 (LOW  表示 壓下按鈕)
  ; 啟動設定
: 室溫參考點 600  \ 或可考慮 不再減的溫度
  ; \ FORGET 80度參考點
: 80度參考點 800 
  ;
: 煮沸參考點 927  \ 或可考慮 不再增的溫度
  ;
: 爐溫 1 ANALOGREAD DUP 3 FOR 8 EMIT NEXT 4 .R ;
  ;
: 電子鍋開始加熱 6 PD HIGH
  ;
: 電子鍋停止加熱 6 PD LOW
  ;
: 燈亮 5 PB HIGH  \ 人工介入提示燈 亮
  ;
: 燈滅 5 PB LOW   \ 人工介入提示燈 滅
  ;
: 按下鈕了 7 PD DIGITALREAD 0 =
  ;
: 等按鈕
  燈亮 BEGIN 按下鈕了 UNTIL 燈滅 
  ;
\ FORGET 維持煮沸五分鐘
: 維持煮沸五分鐘 CR 1500
  FOR 爐溫 煮沸參考點 30 + > 
      IF    電子鍋停止加熱
      ELSE  爐溫 煮沸參考點 30 - < 
         IF 電子鍋開始加熱 
         THEN
      THEN 200 MS 按下鈕了
      IF R> 0 >R
         CR ." 跳過此步驟"
      THEN
  NEXT 電子鍋停止加熱
  ;
: 維持80度十分鐘 CR 3000
  FOR 爐溫 80度參考點 30 + > 
      IF    電子鍋停止加熱
      ELSE  爐溫 80度參考點 30 - < 
         IF 電子鍋開始加熱
         THEN
      THEN 200 MS 按下鈕了
      IF R> 0 >R
         CR ." 跳過此步驟"
      THEN
  NEXT 電子鍋停止加熱
  ;
\ FORGET 製作花瓜
: 製作花瓜 ( -- )
  啟動設定 \ 啟動前 預作 溫度輸入 按鈕輸入 燈號輸出 控制輸出 相關信號 腳位設定
  CR ." ==== 製作花瓜 開始 ===="
  CR ." 00. 請將 金蘭醬油150g 工研烏醋100g 黑糖100g 加入電子鍋 關上蓋子"
  CR ." 01. 請將 安全溫度控制器 連上電源 通電"
  CR ." 02. 請將 安全溫度控制器 爐溫探頭 貼上 電子鍋"
  CR ." 03. 請將 電子鍋 插頭 連於 安全溫度控制器 插座 到時自動通電"
  CR ." 04. 就緒 請按鈕" 
  CR ." 準備 煮沸醬汁五分鐘 然後降溫至80度"
  等按鈕
\ CR ." 控制加溫 ...."
\ 電子鍋開始加熱 BEGIN 1000 MS 爐溫 煮沸參考點 > UNTIL 電子鍋停止加熱
\ CR ." 煮沸維持五分鐘 ...."
\ 維持煮沸五分鐘
  CR ." 05. 請將 蓋子打開 降溫"
  CR ." 06. 就緒 請按鈕" 
  CR ." 準備 降到 80度"
  等按鈕
  CR ." 停電降溫 ...."
  BEGIN 200 MS 爐溫 80度參考點 < 按下鈕了 OR UNTIL 
  CR ." 07. 請將 花瓜片 加入 關上蓋子"
  CR ." 08. 就緒 請按鈕"
  CR ." 準備 維持 80度 十分鐘"
  等按鈕
  CR ." 維持 80度 ...."
  維持80度十分鐘
  CR ." 09. 請將 蓋子打開 取出花瓜片 裝碗納涼"
  CR ." 10. 就緒 請按鈕"
  CR ." 準備降至室溫 ...."
  等按鈕
  BEGIN 200 MS 爐溫 室溫參考點 < 按下鈕了 OR UNTIL
  CR ." 11. 請將 醬汁倒入裝花瓜的碗"
  CR ." 12. 放入冰箱 備用"
  CR ." ==== 製作花瓜 完成 ====" 
  ;
' 製作花瓜 'BOOT 2DUP ! I! FLUSH REMEMBER \ 存為 TURNKEY 
  製作花瓜
